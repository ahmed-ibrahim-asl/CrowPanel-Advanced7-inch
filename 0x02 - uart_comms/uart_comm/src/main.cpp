#include "pins_config.h"
#include "LovyanGFX_Driver.h"   // LGFX class with RGB + GT911

#include <Arduino.h>
#include <lvgl.h>
#include <Wire.h>
#include <SPI.h>

#include "ui.h"                 // generated by SquareLine
#include <TCA9534.h>

LGFX   gfx;
TCA9534 ioex;

/* LVGL draw buffer (single buffer) */
static lv_disp_draw_buf_t draw_buf;
static lv_color_t *buf;         // single buffer
static lv_color_t *buf1 = nullptr;

static const uint16_t SCREEN_HOR_RES = 800;
static const uint16_t SCREEN_VER_RES = 480;

HardwareSerial uart1(1); 


#define SERIAL1_RX 19  // Serial port 1 receives pins
#define SERIAL1_TX 20  // Serial port 1 Send pins


/* ------------ helpers ------------ */
bool i2cScan(uint8_t addr)
{
  Wire.beginTransmission(addr);
  return (Wire.endTransmission() == 0);
}

// Enable panel power / backlight for CrowPanel Advance
void enable_backlight()
{
  bool has_mcu  = i2cScan(0x30);   // new boards
  bool has_ioex = i2cScan(0x18);   // older boards with TCA9534

  Serial.printf("I2C scan: 0x30=%d  0x18=%d\n", has_mcu, has_ioex);

  if (has_mcu) {
    // V1.2+ CrowPanel MCU at 0x30: 0x10 = power, 0x18 = backlight
    Wire.beginTransmission(0x30);
    Wire.write(0x10);
    Wire.endTransmission();

    Wire.beginTransmission(0x30);
    Wire.write(0x18);
    Wire.endTransmission();

    Serial.println("Backlight enabled via 0x30");
  } else if (has_ioex) {
    // Older panels: TCA9534 controls power/backlight
    ioex.attach(Wire);
    ioex.setDeviceAddress(0x18);
    ioex.config(1, TCA9534::Config::OUT);
    ioex.config(2, TCA9534::Config::OUT);

    ioex.output(1, TCA9534::Level::H);   // panel power
    delay(20);
    ioex.output(2, TCA9534::Level::H);   // backlight
    Serial.println("Backlight enabled via TCA9534");
  } else {
    Serial.println("No 0x30 or 0x18 found – cannot enable backlight");
  }
}

/* ------------ LVGL display flush (striped, synchronous) ------------ */
void my_disp_flush(lv_disp_drv_t *disp, const lv_area_t *area, lv_color_t *color_p)
{
  const int32_t x = area->x1;
  const int32_t y = area->y1;
  const int32_t w = area->x2 - area->x1 + 1;
  const int32_t h = area->y2 - area->y1 + 1;
  const int32_t stripe_h = 16;          // 16–32 is usually stable

  lgfx::rgb565_t *src = (lgfx::rgb565_t *)&color_p->full;

  gfx.startWrite();
  for (int32_t row = 0; row < h; row += stripe_h) {
    int32_t ch = (h - row) > stripe_h ? stripe_h : (h - row);
    lgfx::rgb565_t *ptr = src + (size_t)row * (size_t)w;

    gfx.pushImageDMA(x, y + row, w, ch, ptr);
    gfx.waitDMA();                      // wait for this stripe to finish
  }
  gfx.endWrite();

  lv_disp_flush_ready(disp);
}

/* ------------ LVGL touch read ------------ */
void my_touchpad_read(lv_indev_drv_t *indev_driver, lv_indev_data_t *data)
{
  static bool     last_touched = false;
  static uint16_t last_x = 0, last_y = 0;

  uint16_t x, y;
  bool touched = gfx.getTouch(&x, &y);

  if (touched) {
    if (x >= SCREEN_HOR_RES) x = SCREEN_HOR_RES - 1;
    if (y >= SCREEN_VER_RES) y = SCREEN_VER_RES - 1;

    last_touched = true;
    last_x = x;
    last_y = y;

    data->state   = LV_INDEV_STATE_PR;
    data->point.x = last_x;
    data->point.y = last_y;
  } else {
    last_touched  = false;
    data->state   = LV_INDEV_STATE_REL;
  }
}

/* ------------ setup ------------ */
void setup()
{
  Serial.begin(115200);
  delay(200);

  //!
  Serial1.begin(115200, SERIAL_8N1, SERIAL1_RX, SERIAL1_TX);  // Serial port 1 is initialized
  

  // I2C for GT911 + panel MCU/IO expander (SDA=15, SCL=16 as in LGFX)
  Wire.begin(15, 16);
  delay(20);
  enable_backlight();           // turn panel + backlight on

#if CONFIG_SPIRAM_SUPPORT
  if (!psramInit()) {
    Serial.println("PSRAM init failed");
  } else {
    Serial.printf("PSRAM size: %d MB\n", ESP.getPsramSize() / 1024 / 1024);
  }
#endif

  // Initialize display
  gfx.init();
  gfx.initDMA();
  gfx.fillScreen(TFT_BLACK);

  
  // Initialize LVGL
  lv_init();

  const uint32_t lv_lines     = 40;                       // 40 lines per buffer
  const size_t   buffer_px    = (size_t)SCREEN_HOR_RES * lv_lines;
  const size_t   buffer_bytes = sizeof(lv_color_t) * buffer_px;

  // single DMA-capable buffer
  buf = (lv_color_t *)heap_caps_malloc(buffer_bytes, MALLOC_CAP_DMA);
  buf1 = nullptr;

  if (!buf) {
    Serial.println("LVGL buffer allocation failed");
    while (1) delay(1000);
  }

  lv_disp_draw_buf_init(&draw_buf, buf, buf1, buffer_px);

  static lv_disp_drv_t disp_drv;
  lv_disp_drv_init(&disp_drv);
  disp_drv.hor_res  = SCREEN_HOR_RES;
  disp_drv.ver_res  = SCREEN_VER_RES;
  disp_drv.flush_cb = my_disp_flush;
  disp_drv.draw_buf = &draw_buf;
  lv_disp_drv_register(&disp_drv);

  static lv_indev_drv_t indev_drv;
  lv_indev_drv_init(&indev_drv);
  indev_drv.type    = LV_INDEV_TYPE_POINTER;
  indev_drv.read_cb = my_touchpad_read;
  lv_indev_drv_register(&indev_drv);

  // Initialize UI created by SquareLine
  ui_init();

  // Extra label for verification
  lv_obj_t *label = lv_label_create(lv_scr_act());
  lv_obj_align(label, LV_ALIGN_BOTTOM_MID, 0, -10);

  Serial.println("Setup done");
}

/* ------------ loop ------------ */
extern "C" void request_signal(void);
extern "C" void update_temperature_label(void);

void loop()
{
  static unsigned long last_update = 0;
  lv_timer_handler();
  unsigned long now = millis();
  if (now - last_update >= 1000) {
    request_signal();
    update_temperature_label();
    last_update = now;
  }
  delay(5);
}
